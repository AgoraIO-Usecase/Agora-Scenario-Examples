plugins {
    id 'com.android.library'
}
def safeExtGetVersion(prop, key, fallback) {
    if (rootProject.ext.has(prop)) {
        def map = rootProject.ext.get(prop)
        if (map.containsKey(key)) {
            return map.get(key)
        }
    }
    return fallback
}

def safeExtGetDependence(key, fallback){
    return "$key:${safeExtGetVersion('dependencies', key, fallback)}"
}


android {
    compileSdkVersion safeExtGetVersion('android', 'compileSdkVersion', 30)
    buildToolsVersion safeExtGetVersion('android', 'buildToolsVersion', '30.0.3')

    defaultConfig {
        minSdkVersion safeExtGetVersion('android', 'minSdkVersion', 21)
        targetSdkVersion safeExtGetVersion('android', 'targetSdkVersion', 30)
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures{
        viewBinding true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation safeExtGetDependence('junit:junit','4.+')
    androidTestImplementation safeExtGetDependence('androidx.test.ext:junit','1.1.2')
    androidTestImplementation safeExtGetDependence('androidx.test.espresso:espresso-core','3.3.0')
    implementation safeExtGetDependence('androidx.multidex:multidex','2.0.1')

    //androidX
    implementation safeExtGetDependence('androidx.constraintlayout:constraintlayout','2.0.4')
    implementation safeExtGetDependence('androidx.swiperefreshlayout:swiperefreshlayout','1.1.0')
    implementation safeExtGetDependence('androidx.annotation:annotation','1.1.0')
    api safeExtGetDependence('androidx.appcompat:appcompat','1.2.0')
    implementation safeExtGetDependence('androidx.preference:preference','1.1.1')
    api safeExtGetDependence('com.google.android.material:material','1.3.0')
    implementation safeExtGetDependence('androidx.cardview:cardview','1.0.0')
    implementation safeExtGetDependence('androidx.work:work-runtime','2.7.0')

    implementation safeExtGetDependence('pl.droidsonroids.gif:android-gif-drawable','1.2.23')
}